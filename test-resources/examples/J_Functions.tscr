Subst[@Stx_node, @Stx_node, @Stx_node].

&Main[].
\x -> \x &Recurse[&All[]] &Main[];
\x -> \x;

&All[].
\x -> \x &Add[];
\x -> \x &LT[];
\x -> \x &If[];
\x -> \x &Apply[];

&Add[].
'((+ \xAt) \yAt)' -> Scheme_Atom[\xpy],
  \xpy <- js'\x + \y'!,
  Scheme_Atom[\x] <- \xAt,
  Scheme_Atom[\y] <- \yAt;

&LT[].
'((< \xAt) \yAt)' -> Scheme_Atom[\xpy],
  \xpy <- js'\x < \y',
  Scheme_Atom[\x] <- \xAt,
  Scheme_Atom[\y] <- \yAt;

&If[].
'(if #true \true \false)' -> \true;
'(if #false \true \false)' -> \false;

&Apply[].
'((lambda (\x) \body) \arg)' -> Subst[\body, \x, \arg] &Subst[],
  Scheme_Symbol[\_] <- \x;

&Subst[].
Subst[\old, \old, \new] -> \new;
Subst['(lambda (\old) \body)', \old, \_] -> '(lambda (\old) \body)',
  Scheme_Symbol[\_] <- \old;
Subst['(lambda (\x) \body)', \old, \new] -> '(lambda (\x) \body2)',
  Scheme_Symbol[\_] <- \x,
  \body2 <- Subst[\body, \old, \new] &Subst[];
Subst['(\f \x)', \old, \new] -> '(\f2 \x2)',
  \f2 <- Subst[\f, \old, \new] &Subst[],
  \x2 <- Subst[\x, \old, \new] &Subst[];
Subst['(\a \x \y \z)', \old, \new] -> '(\a \x2 \y2 \z2)',
  \x2 <- Subst[\x, \old, \new] &Subst[],
  \y2 <- Subst[\y, \old, \new] &Subst[],
  \z2 <- Subst[\z, \old, \new] &Subst[];
Subst[\body, \_, \_] -> \body;

&NonEmpty[].
Scheme_SCons[\_, \_] -> T[];

&IsEqual[].
T[\x, \x] -> True[];
T[\_, \_] -> False[];

&Recurse[&x].
\x -> \x &x[];
'(\x \y)' -> '(\x2 \y2)',
  True[] <- T[\xr, \yr] &Or[],
  T[\x2, \xr] <- \x &Try[&Recurse[&x[]]],
  T[\y2, \yr] <- \y &Try[&Recurse[&x[]]];
'(list \x \y \z)' -> '(list \x2 \y2 \z2)',
  True[] <- T[T[\xr, \yr], \zr] &Or[],
  T[\x2, \xr] <- \x &Try[&Recurse[&x[]]],
  T[\y2, \yr] <- \y &Try[&Recurse[&x[]]],
  T[\z2, \zr] <- \z &Try[&Recurse[&x[]]];
'(if \x \y \z)' -> '(if \x2 \y \z)',
  True[] <- \xr,
  T[\x2, \xr] <- \x &Try[&Recurse[&x[]]];
'(lambda (\x) \body)' -> '(lambda (\x) \body2)',
  \body2 <- \body &Recurse[&x[]];

&Try[&x].
\x -> T[\y, True[]],
  \y <- \x &x[];
\x -> T[\x, False[]];

&Or[].
T[True[], True[]] -> True[];
T[True[], False[]] -> True[];
T[False[], True[]] -> True[];
T[False[], False[]] -> False[];
T[T[\x, \y], \z] -> T[\xy, \z] &Or[],
  \xy <- T[\x, \y] &Or[];
T[\x, \yzu] -> T[\x, \yz] &Or[],
  T[\_, \_] <- \yzu,
  \yz <- \yzu &Or[];
